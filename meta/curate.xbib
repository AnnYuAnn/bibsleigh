corpus c:\bigrepos\bibsleigh\json\corpus
in */*/*/*

-- If a paper has no venue, inherit from the conference
when no venue
	inherit venue

-- Useless unclickable value, legacy of DBLP importing
for each
	when url =~ db/conf/
		remove url
for each
	when url =~ db/series/
		remove url
for each
	when url =~ db/journals/
		remove url

-- Convert EE to DOI, ACMID and other special fields
when ee =~ https://doi.org/
	rename to doi
when ee =~ https://dl.acm.org/citation.cfm?id=
	rename to acmid
when ee =~ http://doi.acm.org/
	rename to doi

-- Curate DOI and ACMID fields of addresses
for each
	when doi =~ http://doi.acm.org/
		truncate left 19
for each
	when doi =~ https://doi.org/
		truncate left 16
for each
	when acmid =~ https://dl.acm.org/citation.cfm?id=
		truncate left 35

-- Drop booktitle if it's useless
when booktitle == $booktitleshort
	remove booktitle
-- Inherit good booktitle
when no journal
	when booktitleshort
		when no booktitle
			inherit title
when booktitle
	when no booktitleshort
		inherit titleshort
when booktitle =^= titleshort
	inherit title
when booktitle =^= title
	when no booktitleshort
		inherit titleshort
when no journalshort
	when no journal
		when no booktitleshort
			when no booktitle
				inherit title
when journal =^= journalshort
	inherit journal
-- This is to fix previous mistakes, never run this commant on the conference level!
when titleshort
	remove titleshort

-- Fill in the gaps from the parent
when no isbn
	inherit isbn
when no publisher
	inherit publisher

apply known_names

-- Prefer numbers to strings
use the force

-- temp

---------------------------------------------------------------
-- now it's time for rewriting venues, not individual papers --
---------------------------------------------------------------

in */*/*

-- Clones of things above
when url =~ db/conf/
	remove url
when ee =~ https://doi.org/
	rename to doi
when ee =~ https://dl.acm.org/citation.cfm?id=
	rename to acmid
when doi =~ https://doi.org/
	truncate left 16
when acmid =~ https://dl.acm.org/citation.cfm?id=
	truncate left 35

apply known_names

-- Specific to venues!
when booktitle
	rename to titleshort
when titleshort ~= _Conference
	truncate right 11
when titleshort == ACM_Symposium_on_Document_Engineering
	assign DocEng
when no venue
	assign $titleshort

-- Prefer numbers to strings
use the force
